{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 简述兰伯特投影风速旋转问题 \n",
    "上次分享了一篇关于风向调整的文章，发现很多使用WARMS和WARR产品的小伙伴都没有注意这个问题。虽然也学习过这个代码，但是平时还是偷懒使用wrf_user_getvar直接获取。正好很多小伙伴问具体操作问题，借此梳理一下。水平有限，如有不足还请指正。\n",
    "\n",
    "## 旋转问题其实你早就会了\n",
    "抛开模式网格的问题，回到高中，你一定做过关于坐标系旋转的题目：\n",
    "\n",
    ">在原坐标系xoy中,  绕原点沿逆时针方向旋转theta度， 变成座标系 sot。设有某点p，在原坐标系中的坐标为 (x, y), 旋转后的新坐标为(s, t)。\n",
    "\n",
    "![旋转坐标问题](https://upload-images.jianshu.io/upload_images/11801911-0d68a4824aa89147.gif?imageMogr2/auto-orient/strip%7CimageView2/2/w/1240)\n",
    "```\n",
    "oa = y sin(theta)   (2.1)\n",
    "as = x cos(theta)   (2.2)\n",
    "综合(2.1)，(2.2) 2式\n",
    "s =  os = oa + as = x cos(theta) + y sin(theta) \n",
    "t =  ot = ay – ab = y cos(theta) – x sin(theta)\n",
    "```\n",
    "\n",
    "现在，我们已经得到了一个普世的旋转公式，下面的关注点就集中到角度theta上。\n",
    "\n",
    "## 兰伯特投影\n",
    "如图所示，一个锥形投影展开后得到的是一个扇面（$ 2\\pi k $）。我们规定单位经度圆锥面所张的平面角为k，k称为圆锥常数。所以在投影面上，160°E与150°E的差值是【10°*k】。\n",
    "\n",
    "![圆锥投影](https://upload-images.jianshu.io/upload_images/11801911-9431bd51d4a3f427.jpg?imageMogr2/auto-orient/strip%7CimageView2/2/w/1240)\n",
    "\n",
    "在wrf的namelist中，设置兰伯特投影需要重点关注【STDLON、TRUELAT1、（TRUELAT2）】这几个变量。其中STDLION所设置的经线是与模式网格Y轴平行的；TRUELAT1(TRUELAT2)是圆锥投影上的两个割线位置，地图放大系数是1。为此，我们获得了求解角度的两个重要关系。\n",
    "![兰伯特投影面上的计算网格](https://upload-images.jianshu.io/upload_images/11801911-495794796e7d45a3.jpg?imageMogr2/auto-orient/strip%7CimageView2/2/w/1240)\n",
    "\n",
    "\n",
    "- 所有格点上的旋转经度差都以STDLON为基准，记diff=XLONG-STDLON。由计算网格转地球参考坐标过程中：在标准经线西边，是顺时针旋转，旋转角应该是负值；在标准经线东边，是逆时针旋转，旋转角应该是正值。采用diff = XLONG - stand_lon正好满足需求。\n",
    "- 在兰伯特割投影下（暂定为30°N、60°N），则k的公式如下（具体推导见沈桐立《数值天气预报》P60~65）\n",
    "$k = \\frac{\\ln \\sin \\Theta_{1}-\\ln \\sin \\Theta_{2}}{\\ln \\tan \\frac{\\Theta _{1}}{2} -\\ln \\tan \\frac{\\Theta _{2}}{2} }$ 这里的角度为余纬。下面程序中的角度为纬度，做个三角函数变换就行了。\n",
    "\n",
    "至此，我们求得投影面上旋转角度theta=diff*k\n",
    "\n",
    "## 程序解读\n",
    "具体的实现只差一段代码，我们先来看一个简单的例子。稍后再介绍WRF模式中C网格下兰伯特投影风速旋转。\n",
    "\n",
    "#### 1 ARWPOST程序module_cal_uvmet.f90\n",
    "这个程序的目的很明确，即将计算格点的风速UV转换到地球投影上来。\n",
    "```\n",
    "    !! Diagnostics: U & V on earth coordinates\n",
    "\n",
    "    MODULE module_calc_uvmet\n",
    "\n",
    "    CONTAINS\n",
    "    SUBROUTINE calc_uvmet(SCRa, SCRb, cname, cdesc, cunits, i3dflag)\n",
    "\n",
    "    USE constants_module\n",
    "    USE module_model_basics\n",
    "\n",
    "    IMPLICIT NONE\n",
    "\n",
    "    !Arguments\n",
    "    real, allocatable, dimension(:,:,:)             :: SCRa, SCRb\n",
    "    character (len=128)                             :: cname, cdesc, cunits\n",
    "\n",
    "    !Local\n",
    "    integer                                         :: i, j, k\n",
    "    integer                                         :: i3dflag\n",
    "    real                                            :: cone\n",
    "    real, dimension(west_east_dim,south_north_dim)  :: diff, alpha\n",
    "\n",
    "    cname    = \"uvmet\"\n",
    "    cdesc    = \"Rotated wind component\"\n",
    "    cunits   = \"m s-1\"\n",
    "    \n",
    "    \n",
    "    IF ( map_proj .ge. 3 ) THEN     ! No need to rotate\n",
    "        IF ( i3dflag == 1 ) THEN  \n",
    "        SCRa = UUU\n",
    "        SCRb = VVV\n",
    "        ENDIF\n",
    "        IF ( i3dflag == 0 ) THEN\n",
    "        SCRa(:,:,1) = U10(:,:)\n",
    "        SCRb(:,:,1) = V10(:,:)\n",
    "        END IF\n",
    "        RETURN\n",
    "    END IF\n",
    "\n",
    "!求解圆锥割投影和圆锥切投影的圆锥常数\n",
    "    cone = 1.                                          !  PS\n",
    "    IF ( map_proj .eq. 1) THEN                         !  Lambert Conformal mapping\n",
    "        IF (ABS(truelat1-truelat2) .GT. 0.1) THEN\n",
    "        cone=(ALOG(COS(truelat1*RAD_PER_DEG))-            &\n",
    "                ALOG(COS(truelat2*RAD_PER_DEG))) /          &\n",
    "        (ALOG(TAN((90.-ABS(truelat1))*RAD_PER_DEG*0.5 ))- &\n",
    "            ALOG(TAN((90.-ABS(truelat2))*RAD_PER_DEG*0.5 )) )\n",
    "        ELSE\n",
    "        cone = SIN(ABS(truelat1)*RAD_PER_DEG )\n",
    "        ENDIF\n",
    "    END IF\n",
    "\n",
    "!求解经度差值，转换范围到（-180,180）\n",
    "    diff = XLONG - stand_lon\n",
    "    DO i = 1, west_east_dim\n",
    "    DO j = 1, south_north_dim\n",
    "        IF ( diff(i,j) .gt. 180. ) THEN\n",
    "        diff(i,j) = diff(i,j) - 360.\n",
    "        END IF\n",
    "        IF ( diff(i,j) .lt. -180. ) THEN\n",
    "        diff(i,j) = diff(i,j) + 360.\n",
    "        END IF\n",
    "    END DO\n",
    "    END DO\n",
    "\n",
    "!求解旋转角，北半球系数为1，南半球系数为-1\n",
    "    DO i = 1, west_east_dim\n",
    "    DO j = 1, south_north_dim\n",
    "        IF ( XLAT(i,j) .lt. 0. ) THEN\n",
    "        alpha(i,j) = - diff(i,j) * cone * RAD_PER_DEG\n",
    "        ELSE\n",
    "        alpha(i,j) = diff(i,j) * cone * RAD_PER_DEG\n",
    "        END IF\n",
    "    END DO\n",
    "    END DO\n",
    "\n",
    "    \n",
    "!使用旋转角公式旋转。\n",
    "    IF ( i3dflag == 1 ) THEN\n",
    "        DO k = 1,bottom_top_dim\n",
    "        SCRa(:,:,k) = VVV(:,:,k)*sin(alpha) + UUU(:,:,k)*cos(alpha)\n",
    "        SCRb(:,:,k) = VVV(:,:,k)*cos(alpha) - UUU(:,:,k)*sin(alpha)\n",
    "        END DO\n",
    "    ELSE\n",
    "        SCRa(:,:,1) = V10(:,:)*sin(alpha) + U10(:,:)*cos(alpha)\n",
    "        SCRb(:,:,1) = V10(:,:)*cos(alpha) - U10(:,:)*sin(alpha)\n",
    "    END IF\n",
    "\n",
    "    END SUBROUTINE calc_uvmet\n",
    "\n",
    "    END MODULE module_calc_uvmet\n",
    "\n",
    "```\n",
    "相对的，将地球坐标上的UVMET换到格点UV上，我们依然使用上述程序。只不过旋转的方向变了。所以角度需要乘以-1。\n",
    "#### 2 WPS程序rotate_winds_module.f90\n",
    "相比较ARWPOST中程序，WPS中的代码将这个问题描述的更加全面`/WPS/metgrid/src/rotate_winds_module.f90`。在ARWPOST中，风速应该是已经先被处理到了质量格点。在WRF中，由于程序是针对C网格设计，考虑了uv格点数目不同，位置不同，会涉及到格点的插值等一些问题以及边界上点处理的问题，但是核心内容依然如ARWPOST中所述。\n",
    "\n",
    "```\n",
    "   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! \n",
    "   ! Name: metmap_xform                                                           !\n",
    "   !                                                                              !\n",
    "   ! Purpose: Do the actual work of rotating winds for C grid.                    !\n",
    "   !          If idir= 1, rotate grid-relative winds to Earth-relative winds      !\n",
    "   !          If idir=-1, rotate Earth-relative winds to grid-relative winds      !\n",
    "   !                                                                              !\n",
    "   ! ASSUMPTIONS: 1) MEMORY ORDER IS XY.                                          !\n",
    "   !              2) U ARRAY HAS ONE MORE COLUMN THAN THE V ARRAY, AND V ARRAY    !\n",
    "   !                 HAS ONE MORE ROW THAN U ARRAY.                               !\n",
    "   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! \n",
    "...\n",
    "...\n",
    "...\n",
    "         ! Only rotate winds for Lambert conformal, polar stereographic, or Cassini  \n",
    "         !! idir=1时是有计算格点到地球参考，-1时是地球参考到计算格点\n",
    "         if ((proj_stack(current_nest_number)%code == PROJ_LC) .or. &\n",
    "             (proj_stack(current_nest_number)%code == PROJ_PS) .or. &\n",
    "             (proj_stack(current_nest_number)%code == PROJ_CASSINI)) then\n",
    "            call mprintf((idir ==  1),LOGFILE,'Rotating map winds to earth winds.')\n",
    "            call mprintf((idir == -1),LOGFILE,'Rotating earth winds to grid winds')\n",
    "...\n",
    "...\n",
    "...\n",
    "            ! Rotate U field \n",
    "            !!diff即经度差,xlon_u(i,j)为C网格上U格点的经度，不同于M格点。\n",
    "            !!proj_stack(current_nest_number)%stdlon为当前嵌套网格的标准经度。\n",
    "            !!proj_stack(current_nest_number)%cone即为圆锥系数，具体和ARWPOST中程序相似。\n",
    "            !!proj_stack(current_nest_number)%hemi为南北半球系数，北半球为1，南半球为-1。\n",
    "                  diff = idir * (xlon_u(i,j) - proj_stack(current_nest_number)%stdlon)\n",
    "                  if (diff > 180.) then\n",
    "                     diff = diff - 360.\n",
    "                  else if (diff < -180.) then\n",
    "                     diff = diff + 360.\n",
    "                  end if\n",
    "\n",
    "                  ! Calculate the rotation angle, alpha, in radians \n",
    "                  if (proj_stack(current_nest_number)%code == PROJ_LC) then\n",
    "                     alpha = diff * proj_stack(current_nest_number)%cone * rad_per_deg * proj_stack(current_nest_number)%hemi \n",
    "...\n",
    "...\n",
    "...\n",
    "                  ! On C grid, take U_ij, and get V value at the same lat/lon  v为四点权重插值\n",
    "                  !   by averaging the four surrounding V points\n",
    "                        u_new(i,j) = cos(alpha)*u_map + sin(alpha)*v_map/v_weight                 \n",
    "...\n",
    "...\n",
    "            ! Rotate V field 类似\n",
    "                        v_new(i,j) = -sin(alpha)*u_map/u_weight + cos(alpha)*v_map\n",
    "...\n",
    "...\n",
    "... \n",
    "   end subroutine metmap_xform\n",
    "```\n",
    "将WPS中的idir接入ARWPOST中，就可以完善整个旋转问题。"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
